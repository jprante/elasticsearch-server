apply plugin: 'maven'
apply plugin: 'signing'

configurations {
    wagon
}

dependencies {
    wagon "org.apache.maven.wagon:wagon-ssh:${property('wagon.version')}"
}

task xbibUpload(type: Upload) {
    group = 'publish'
    configuration = configurations.archives
    uploadDescriptor = true
    repositories {
        if (project.hasProperty('xbibUsername')) {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment ->
                    signing {
                        signPom(deployment)
                    }
                }
                configuration = configurations.wagon
                repository(url: uri(project.property('xbibUrl'))) {
                    authentication(userName: xbibUsername, privateKey: xbibPrivateKey)
                }
            }
        }
    }
}

task sonatypeUpload(type: Upload) {
    group = 'publish'
    configuration = configurations.archives
    uploadDescriptor = true
    repositories {
        if (project.hasProperty('ossrhUsername')) {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment ->
                    signing {
                        signPom(deployment)
                    }
                }
                repository(url: uri(ossrhReleaseUrl)) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
                snapshotRepository(url: uri(ossrhSnapshotUrl)) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
                pom.project {
                    groupId project.group
                    artifactId project.name
                    version project.version
                    name project.name
                    description description
                    packaging 'jar'
                    inceptionYear '2018'
                    url 'https://github.com/jprante/elasticsearch-server'
                    organization {
                        name 'xbib'
                        url 'http://xbib.org'
                    }
                    developers {
                        developer {
                            id user
                            name 'JÃ¶rg Prante'
                            email 'joergprante@gmail.com'
                            url 'https://github.com/jprante'
                        }
                    }
                    scm {
                        url 'https://github.com/jprante/elasticsearch-server'
                        connection 'scm:git:git@github.com/jprante/elasticsearch-server.git'
                        developerConnection 'scm:git:git@github.com/jprante/elasticsearch-server.git'
                    }
                    licenses {
                        license {
                            name 'Apache License Version 2'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                }
            }
        }
    }
}

nexusStaging {
    packageGroup = "org.xbib"
}

